<p>In this post we'll do a bit of exploration with <a target="_blank" class="link_small" href="https://elixir-lang.org">Elixir</a> macros and create our own little HTML DSL that will be part of a larger exploration project that develops a simple MVC based web framework.</p>

<p>This DSL should have a frontend and a backend that actually generates the HTML representation. For now it should use <a target="_blank" class="link_small" href="https://github.com/zambal/eml">Eml</a> to generate the HTML representation and the to_string conversion.<br>
However, it would be possible to also create an implementation that uses <a target="_blank" class="link_small" href="https://hexdocs.pm/eex/EEx.html">EEx</a> as a backend. And we could switch the backend without having the API user change its code.</p>

<p>So here is what we have to do to create a HTML DSL.</p>

<p>First we need a collection of tags. I have hardcoded them into a list:</p>

<div><pre class="brush: elixir;">  @tags [:html, :head, :title, :base, :link, :meta, :style,
         :script, :noscript, :body, :div, :span, :article, ...]</pre></div>

<p>Then I want to allow to define tags in two styles. A one-liner style and a style with a multi-line body to be able to express multiple child elements.</p>

<div><pre class="brush: elixir;"># one-liner
span id: &quot;1&quot;, class: &quot;span-class&quot;, do: &quot;my span text&quot;

# multi-liner
div id: &quot;1&quot;, class: &quot;div-class&quot; do
  span do: &quot;my span text&quot;
  span do: &quot;my second text&quot;
end</pre></div>

<p>We need two macros for this. The <code>do:</code> in the one-liner is seen just as an attribute to the macro. So we have to strip out the <code>do:</code> attribute and use it as body. The macro for this looks like this:</p>

<div><pre class="brush: elixir;">  defmacro tag(name, attrs \\ []) do
    {inner, attrs} = Keyword.pop(attrs, :do)
    quote do: HouseStatUtil.HTML.tag(unquote(name), unquote(attrs), do: unquote(inner))
  end</pre></div>

<p>First we extract the value for the <code>:do</code> key in the <code>attrs</code> list and then pass the <code>name</code>, the remaining <code>attrs</code> and the extracted body as <code>inner</code> to the actual macro which looks like this and does the whole thing.</p>

<div><pre class="brush: elixir;">  defmacro tag(name, attrs, do: inner) do
    parsed_inner = parse_inner_content(inner)
    
    quote do
      %E{tag: unquote(name),
         attrs: Enum.into(unquote(attrs), %{}),
         content: unquote(parsed_inner)}
    end
  end

  defp parse_inner_content({:__block__, _, items}), do: items
  defp parse_inner_content(inner), do: inner</pre></div>

<p>Here we get the first glimpse of Eml (the <code>%E{}</code> in there is an Eml structure type to create HTML tags). The helper function is to differentiate between having an AST as inner block or non-AST elements. But I don&#39;t want to go into more detail here.<br>
Instead I recommend reading the book <a target="_blank" class="link_small" href="https://pragprog.com/book/cmelixir/metaprogramming-elixir">Metaprogrammning Elixir</a> by Chris McCord which deals a lot with macros and explains how it works.</p>

<p>But something is still missing. We now have a <code>tag</code> macro. With this macro we can create HTML tags like this:</p>

<div><pre class="brush: elixir;">tag &quot;span&quot;, id: &quot;1&quot;, class: &quot;class&quot;, do: &quot;foo&quot;</pre></div>

<p>But that&#39;s not yet what we want. One step is missing. We have to create macros for each of the defined HTML tags. Remember the list of tags from above. Now we take this list and create macros from the atoms in the list like so:</p>

<div><pre class="brush: elixir;">  for tag &lt;- @tags do
    defmacro unquote(tag)(attrs, do: inner) do
      tag = unquote(tag)
      quote do: HouseStatUtil.HTML.tag(unquote(tag), unquote(attrs), do: unquote(inner))
    end

    defmacro unquote(tag)(attrs \\ []) do
      tag = unquote(tag)
      quote do: HouseStatUtil.HTML.tag(unquote(tag), unquote(attrs))
    end
  end</pre></div>

<p>This creates three macros for each tag. I.e. for <code>span</code> it creates: <code>span/0</code>, <code>span/1</code> and <code>span/2</code>. The first two are because the <code>attrs</code> are optional but Elixir creates two function signatures for it. The third is a version that has a <code>do</code> block.</p>

<p>With all this put together we can create HTML as Elixir language syntax.
Checkout the full <a target="_blank" class="link_small" href="https://github.com/mdbergmann/elixir_house_stat_util/blob/master/lib/house_stat_util/html.ex">module source</a> in the github repo.</p>

<h4 id="toc_16">Testing the DSL</h4>

<p>Of course we test this. This is a test case for a one-liner tag:</p>

<div><pre class="brush: elixir;">  test &quot;single element with attributes&quot; do
    elem = input(id: &quot;some-id&quot;, name: &quot;some-name&quot;, value: &quot;some-value&quot;)
    |&gt; render_to_string

    IO.inspect elem

    assert String.starts_with?(elem, &quot;&lt;input&quot;)
    assert String.contains?(elem, ~s(id=&quot;some-id&quot;))
    assert String.contains?(elem, ~s(name=&quot;some-name&quot;))
    assert String.contains?(elem, ~s(value=&quot;some-value&quot;))
    assert String.ends_with?(elem, &quot;/&gt;&quot;)
  end</pre></div>

<p>This should be backend agnostic. So no matter which backend generated the HTML we want to see the test pass.</p>

<p>Here is a test case with inner tags:</p>

<div><pre class="brush: elixir;">  test &quot;multiple sub elements - container&quot; do
    html_elem = html class: &quot;foo&quot; do
      head
      body class: &quot;bar&quot;
    end
    |&gt; render_to_string

    IO.inspect html_elem

    assert String.ends_with?(html_elem, 
      ~s(&lt;html class=&quot;foo&quot;&gt;&lt;head&gt;&lt;/head&gt;&lt;body class=&quot;bar&quot;&gt;&lt;/body&gt;&lt;/html&gt;))
  end</pre></div>

<p>The source file has more tests, but that should suffice as examples.</p>

<p>That was it. Thanks for reading.</p>
